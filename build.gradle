plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id 'me.champeau.mrjar' version '0.1.1'
}

group = "com.maths22"
version = "0.1.2"

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

multiRelease {
    targetVersions 8, 17, 18, 19, 20, 21, 22
}

tasks.withType(JavaCompile).configureEach {
    if(it.sourceCompatibility == '19' || it.sourceCompatibility == '20' || it.sourceCompatibility == '21') {
        options.compilerArgs += "--enable-preview"
    }
}

compileJava17Java {
    options.compilerArgs += ["--add-modules", "jdk.incubator.foreign"]
}

compileJava18Java {
    options.compilerArgs += ["--add-modules", "jdk.incubator.foreign"]
}

compileJava19Java {
    options.compilerArgs += "--enable-preview"
}

compileJava20Java {
    options.compilerArgs += "--enable-preview"
}

compileJava21Java {
    options.compilerArgs += "--enable-preview"
}

dependencies {
}

publishing {
    repositories {
        maven {
            name = "fury"
            url = "https://maven.fury.io/maths22/"

            authentication {
                basic(BasicAuthentication)
            }

            credentials {
                username = System.getenv("GEMFURY_TOKEN")
                password = "NOPASS"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit4 test framework
            useJUnit('4.13.2')
        }
    }
}

if((System.getenv("FORCE_WINDOWS_FALLBACK") ?: "0") != "1") {
    java17Test {
        jvmArgs += ["--enable-native-access=ALL-UNNAMED", "--add-modules", "jdk.incubator.foreign"]
    }

    java18Test {
        jvmArgs += ["--enable-native-access=ALL-UNNAMED", "--add-modules", "jdk.incubator.foreign"]
    }

    java19Test {
        jvmArgs += ["--enable-native-access=ALL-UNNAMED", "--enable-preview"]
    }

    java20Test {
        jvmArgs += ["--enable-native-access=ALL-UNNAMED", "--enable-preview"]
    }

    java21Test {
        jvmArgs += ["--enable-native-access=ALL-UNNAMED", "--enable-preview"]
    }

    java22Test {
        jvmArgs += ["--enable-native-access=ALL-UNNAMED"]
    }
}